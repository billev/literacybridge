<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml"
	verticalAlign="middle"
	horizontalGap="0"
	initialize="init()">
	
	<mx:Metadata>
		[Event(name="removeActionEvent", type="org.literacybridge.authoring.events.RemoveActionEvent")] 
	</mx:Metadata>

	
	<mx:Script>
		<![CDATA[
			import org.literacybridge.authoring.events.RemoveActionEvent;
			import org.literacybridge.authoring.schema.actions.SimpleAction;
			import mx.events.CloseEvent;
			import mx.managers.PopUpManager;
			import mx.binding.utils.BindingUtils;
			import org.literacybridge.authoring.schema.actions.Action;
			import mx.controls.Alert;
			import mx.controls.HTML;	
			
			[Bindable] private var text:String;
			[Bindable] public var action:Action;
			private var addAction:AddActionDialog = null;
			
			private function onMouseOver():void {
				setStyle("backgroundColor", btn.getStyle("rollOverColor"));
			}
			
			private function onMouseOut():void {
				setStyle("backgroundColor", "#ffffff");
			}
			
			private function editAction():void {
				addAction = AddActionDialog(PopUpManager.createPopUp(this, AddActionDialog, true));
				addAction.setAction(action);
				addAction.x = mouseX;
				addAction.y = mouseY;
				addAction.addEventListener(CloseEvent.CLOSE, popupClosing);
			}
			
			private function removeAction():void {
				dispatchEvent(new RemoveActionEvent(action));
			}
			
			private function setActionDisplayText(a:Object):void {
				textBox.htmlText=ActionDisplayTextGenerator.getDisplayTextAsHtml(a as Action); 
				if (a is SimpleAction) {
					editButton.visible = false;
				} else {
					editButton.visible = true;
				}
			}

			private function init():void {
				BindingUtils.bindSetter(setActionDisplayText, this, "action");
			}
			
			private function popupClosing(e:Event):void {
				setActionDisplayText(action); // force text update
				addAction.removeEventListener(CloseEvent.CLOSE, popupClosing);
				PopUpManager.removePopUp(addAction);
			}
		]]>
	</mx:Script>
	
	<mx:Text width="100%" id="textBox"/>
	
	<mx:LinkButton id="editButton" icon="@Embed(source='/../assets/pen.png')" toolTip="Edit Action" mouseOver="onMouseOver()" mouseOut="onMouseOut()" click="editAction()"/>
	<mx:LinkButton label="X" fontSize="14" id="btn" mouseOver="onMouseOver()" toolTip="Remove Action" mouseOut="onMouseOut()" color="#D00808" click="removeAction()"/>
</mx:HBox>
